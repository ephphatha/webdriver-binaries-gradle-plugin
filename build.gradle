plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'codenarc'
    id 'com.github.erdi.idea-base' version '2.2'
    id 'com.github.erdi.extended-idea' version '2.2'
    id 'com.gradle.plugin-publish' version '0.12.0'
    id 'com.energizedwork.asl2' version '1.1'
}

apply from: 'gradle/idea/idea.gradle'

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    codenarc('org.codenarc:CodeNarc:1.0') {
        exclude module: 'GMetrics'
        exclude group: 'org.codehaus.groovy'
    }
    codenarc 'org.codehaus.groovy:groovy-all:2.5.12'

    implementation 'org.ysb33r.gradle:grolifant50:1.3.2'
    implementation 'org.apache.maven:maven-artifact:3.6.3'

    testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5', {
        exclude module: 'groovy'
    }
}

version = '2.4.1'

codenarc.configFile = file('gradle/codenarc/rulesets.groovy')

task endToEndTest(type: Test) {
    options {
        includeCategories 'com.github.erdi.gradle.webdriver.category.EndToEnd'
    }
}

tasks.withType(Test) {
    maxHeapSize = "1g"
    jvmArgs '-XX:MaxMetaspaceSize=128m'
    testLogging {
        exceptionFormat = 'full'
    }
}

tasks.withType(GroovyCompile) {
    groovyOptions.forkOptions.memoryMaximumSize = '256m'
}

gradlePlugin {
    plugins {
        webdriverBinariesPlugin {
            id = 'com.github.erdi.webdriver-binaries'
            implementationClass = 'com.github.erdi.gradle.webdriver.WebDriverBinariesPlugin'
        }
    }
}

pluginBundle {
    vcsUrl = 'https://github.com/erdi/webdriver-binaries-gradle-plugin'

    plugins {
        webdriverBinariesPlugin {
            website = 'https://github.com/erdi/webdriver-binaries-gradle-plugin/blob/master/README.md'
            description = 'A plugin that downloads and caches WebDriver binaries specific to the OS the build runs on.'
            id = 'com.github.erdi.webdriver-binaries'
            displayName = 'WebDriver Binaries Plugin'
            tags = ['WebDriver', 'selenium', 'test', 'chromedriver', 'geckodriver']
        }
    }
}